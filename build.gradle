// TODO Publishing: https://guides.gradle.org/publishing-plugins-to-gradle-plugin-portal/
plugins {
    id 'java-gradle-plugin'
    id 'eclipse'
    id 'jacoco'
    id 'checkstyle'
}

// Putting this in gradle.properties breaks the build, because this uses it's own releases
group = 'org.starchartlabs.flare'

description = 'Gradle plug-ins with opinioniated defaults to allow streamlined, consistent operations'

// Always download sources, to allow debugging, and use Eclipse containers for greater portability
eclipse {
    classpath { 
        downloadSources = true 
    }
}

// JCenter is the Gradle plug-in standard
repositories { 
    jcenter() 
}

dependencies { 
    compile gradleApi() 
    compile 'org.starchartlabs.alloy:alloy-core:0.4.1'
    
    testCompile gradleTestKit()
    testCompile 'org.mockito:mockito-core:2.25.0'
    testCompile 'org.testng:testng:6.14.3'
}

// Setup default test behavior, including failure logging
test {
    useTestNG() { 
        useDefaultListeners = true 
    }
}

jacocoTestReport {
    reports {
        executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
        
        sourceSets project.sourceSets.main
        
        xml.enabled true
        csv.enabled false
        html.enabled false
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

jacocoTestReport.dependsOn test
check.dependsOn jacocoTestReport

checkstyle {
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    configProperties = [ 'checkstyle.config.dir' : rootProject.file('config/checkstyle') ]
    toolVersion = '8.18'
}
 
task checkstyleAll{}
 
tasks.withType(Checkstyle).all { checkstyleTask -> checkstyleAll.dependsOn checkstyleTask }
 
check.dependsOn checkstyleAll
